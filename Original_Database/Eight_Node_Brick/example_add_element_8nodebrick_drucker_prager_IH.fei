//essi test suite
//developed by Babak Kamrani
//babakkamrani@yahoo.com
//------------------------------------

// Model Definition:
//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
model name "Eight_Node_Brick_Triaxial_DPIH";


// Constitutive Model
add material # 1 type druckerprager_isotropic_hardening
                 mass_density = 0.0*kg/m^3 
                 elastic_modulus = 1.0e4*N/m^2 
                 poisson_ratio = 0.25
                 druckerprager_angle = 1.0
                 isotropic_hardening_rate = 1.0
                 initial_confining_stress = 0.0*N/m^2
                 algorithm = explicit
                 number_of_subincrements = 1
                 maximum_number_of_iterations = 40
                 tolerance_1 = 1.0e-7
                 tolerance_2 = 1.0e-7;


// Nodal  Coordinates
add node # 1 at (1*m, 1*m , 1*m) with 3 dofs;      
add node # 2 at (0*m, 1*m , 1*m) with 3 dofs;      
add node # 3 at (0*m, 0*m , 1*m) with 3 dofs;      
add node # 4 at (1*m, 0*m , 1*m) with 3 dofs;      
add node # 5 at (1*m, 1*m , 0*m) with 3 dofs;      
add node # 6 at (0*m, 1*m , 0*m) with 3 dofs;      
add node # 7 at (0*m, 0*m , 0*m) with 3 dofs;      
add node # 8 at (1*m, 0*m , 0*m) with 3 dofs; 


// Elements
add element # 1 type 8NodeBrick with nodes (1,2,3,4,5,6,7,8) use material # 1;



// Constrains
fix node No 7 dofs all;
fix node No 2 dofs ux;
fix node No 3 dofs ux uy;
fix node No 4 dofs uy;
fix node No 5 dofs uz;
fix node No 6 dofs ux uz;
fix node No 8 dofs uy uz;


//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Confinement Loading.. 
//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
new loading stage "confinement";
add load # 1  to node # 1 type linear Fx = -25.0*N;
add load # 2  to node # 1 type linear Fy = -25.0*N;
add load # 3  to node # 1 type linear Fz = -25.0*N;
add load # 4  to node # 2 type linear Fy = -25.0*N;
add load # 5  to node # 2 type linear Fz = -25.0*N;
add load # 6  to node # 3 type linear Fz = -25.0*N;
add load # 7  to node # 4 type linear Fx = -25.0*N;
add load # 8  to node # 4 type linear Fz = -25.0*N;
add load # 9  to node # 5 type linear Fx = -25.0*N;
add load # 10 to node # 5 type linear Fy = -25.0*N;
add load # 11 to node # 6 type linear Fy = -25.0*N;
add load # 12 to node # 8 type linear Fx = -25.0*N;





define load factor increment 0.01;
define algorithm with_no_convergence_check;
define solver ProfileSPD;
simulate 100 steps using static algorithm;


//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Shear Loading.. 
//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
new loading stage "shear";

// load
add load # 13 to node # 1 type linear Fz = -100*N;
add load # 14 to node # 2 type linear Fz = -100*N;
add load # 15 to node # 3 type linear Fz = -100*N;
add load # 16 to node # 4 type linear Fz = -100*N;



define static integrator displacement_control using node # 1 dof uz increment -0.0001*m;
define algorithm with_no_convergence_check;
define solver ProfileSPD;
simulate 250 steps using static algorithm;


bye;



